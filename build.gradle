plugins {
    id 'dev.architectury.loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
}

base.archivesName = project.mod_id
group = project.mod_group
version = project.mod_version

loom {
    silentMojangMappingsLicense()
    accessWidenerPath = file("src/main/resources/${project.mod_id}.accesswidener")
    forge.mixinConfig "${project.mod_id}.mixins.json"
}

repositories {
    maven { url = "https://maven.parchmentmc.org" }
    maven { url = "https://maven.bawnorton.com/releases" }
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    minecraft "net.minecraft:minecraft:$project.minecraft_version"
//    mappings loom.officialMojangMappings()
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip")
    }
    forge "net.minecraftforge:forge:$project.minecraft_version-$project.forge_version"

    compileOnly(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:0.3.7-beta.1"))
    include(implementation("com.github.bawnorton.mixinsquared:mixinsquared-forge:0.3.7-beta.1"))

    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
    include(implementation("io.github.llamalad7:mixinextras-forge:0.5.0"))

    implementation("maven.modrinth:rediscovered:2.1.0")
}

processResources {
    filesMatching('META-INF/mods.toml') {
        expand project.properties
    }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}